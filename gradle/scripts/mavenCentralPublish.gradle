/* Maven publish - start */
task sourcesJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier "javadoc"
  from javadoc.destinationDir
}


if (project.hasProperty('user') && project.hasProperty('password') && project.hasProperty('deployVersion')) {

  // snapshot version differs from normal version
  String versionString = project.getProperty('deployVersion')

  signing {
    required { !versionString.endsWith('SNAPSHOT') }
    if (required)
      sign(publishing.publications)
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {

        versionMapping {
          // resolves dynamic versioning to current version number
          usage('java-api') {
            fromResolutionOf('runtimeClasspath')
          }
          usage('java-runtime') {
            fromResolutionResult()
          }
        }
        pom {
          description = 'A small library that provides functionalities to scrape different data from LinkedIn'
          name = 'LinkedIn Scraper'
          url = 'https://github.com/johanneshiry/linkedin-scraper'
          issueManagement {
            system = 'GitHub'
            url = 'https:github.com/johanneshiry/linkedin-scraper/issues'
          }
          licenses {
            license {
              name = ' AGPL-3.0 License'
              url = 'https://github.com/johanneshiry/linkedin-scraper/blob/main/LICENSE'
            }
            scm {
              connection = 'scm:git:git:github.com/johanneshiry/linkedin-scraper.git'
              developerConnection = 'scm:git:ssh:github.com:johanneshiry/linkedin-scraper.git'
              url = 'https://github.com/johanneshiry/linkedin-scraper'
            }
            developers {
              [
                developer {
                  id = 'johanneshiry'
                  name = 'Johannes Hiry'
                  email = 'dev@johannes-hiry.de'
                }
              ]
            }
          }
        }

        removeTestDependenciesFromPom(pom)
        groupId group
        artifactId 'linkedin-scraper'
        version versionString

        from components.java
        artifact sourcesJar
        artifact javadocJar

      }
    }
    repositories {
      maven {
        def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        url = versionString.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username project.getProperty('user')
          password project.getProperty('password')
        }
      }
    }
  }


  model {
    tasks.generatePomFileForMavenJavaPublication {
      destination = file("$buildDir/generated-pom.xml")
    }
  }
}

def removeTestDependenciesFromPom(pom) {

  pom.withXml {
    def root = asNode()
    // eliminate test-scoped dependencies (no need in maven central POMs)
    root.dependencies.removeAll { dep ->
      dep.scope == "test"
    }
  }
}

/* Maven publish - end */
